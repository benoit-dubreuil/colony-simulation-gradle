apply from: file('constants-init.gradle')

buildscript {
    repositories {
        mavenCentral()
    }
}

allprojects {
    apply plugin: 'idea'

    group 'com.cheesygames.colonysimulation'
    version '0.0.1-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.9
    targetCompatibility = 1.9

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()

        maven {
            url "https://dl.bintray.com/stephengold/org.jmonkeyengine/"
        }
    }

    dependencies {
        compile getJmeLocalMavenDependency('core')

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.2.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.2.0'
    }

    test {
        useJUnitPlatform()

        // show standard out and standard error of the test JVM(s) on the console
        testLogging.showStandardStreams = true
    }
}

configure(subprojects.findAll { it.name.contains('library') || it.name == 'assets' }) {
    apply plugin: 'java-library'
}

configure(subprojects.findAll { it.name.contains('game') }) {
    dependencies {
        compile group: 'com.badlogicgames.gdx', name: 'gdx', version: '1.9.8'
        compile group: 'com.badlogicgames.gdx', name: 'gdx-ai', version: '1.8.1'
    }
}

configure(subprojects.findAll { it.name != 'assets' }) {
    dependencies {
        compile getJmeLocalMavenDependency('lwjgl3')
    }
}

configure(subprojects.findAll { it.name != 'game-library' }) {
    dependencies {
        compile project(':game-library')
    }
}

configure(project(':game')) {
    apply plugin: 'application'

    task runApp(type: JavaExec) {
        classpath = sourceSets.main.runtimeClasspath
        enableAssertions = true
    }

    task debugApp(type: JavaExec) {
        classpath = sourceSets.main.runtimeClasspath
        enableAssertions = true
        debug = true
    }

    dependencies {
        compile getJmeLocalMavenDependency('desktop')
        compile project(path: ':assets', configuration: 'assetLibrary')
        compile group: 'com.sudoplay.joise', name: 'joise', version: '1.1.0'
    }
}